
import (
	"std.um"
	"common.um"
	"update.um"
	"../umbox/io/io.um"
)

fn run*(url: str, argi: int) {
	if argi != std.argc() - 1 {
		fprintf(std.stderr(), "Usage: umbox install <box_name>\n")
		return
	}
	
	box := common.parseDep(std.argv(argi))

	ok, meta := common.getMeta("box.json") 
	if !ok {
		fprintf(std.stderr(), "Not in an UmBox directory.\n")
		return
	}

	if !box.exists() {
		fprintf(std.stderr(), "Box '%s' does not exist.\n", box.getName())
		return
	}

	exists := false
	for i,dep in meta.dependencies {
		if dep.getName() == box.getName() {
			exists = true
			break
		}
	}

	if !exists {
		meta.dependencies = append(meta.dependencies, box)
	}

	f := std.fopen("box.json", "w")
	fprintf(f, "%s", meta.toJSON())
	std.fclose(f)

	update.run(url, std.argc())
}
