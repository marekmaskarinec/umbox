
import (
	"std.um"
	"common.um"
	"update.um"
	"../umbox/io/io.um"
)

fn run*(url: str, argi: int) {
	if argi != std.argc() - 1 {
		fprintf(std.stderr(), "Usage: umbox install <box_name>\n")
		return
	}
	
	name := std.argv(argi)

	ok, meta := common.getMeta("box.json") 
	if !ok {
		fprintf(std.stderr(), "Not in an UmBox directory.\n")
		return
	}

	if !common.exists(name) {
		fprintf(std.stderr(), "Box '%s' does not exist.\n", name)
		return
	}

	exists := false
	for i,dep in meta.dependencies {
		if dep == name {
			exists = true
			break
		}
	}

	if !exists {
		meta.dependencies = append(meta.dependencies, name)
	}

	f := std.fopen("box.json", "w")
	fprintf(f, "%s", meta.toJSON())
	std.fclose(f)

	update.run(url, std.argc())
}
