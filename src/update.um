
import (
	"../umbox/os/os.um"	
	"../umbox/json/json.um"
	"../umbox/filepath/filepath.um"
	"../umbox/tar/tar.um"
	"../umbox/jsonenc/jsonenc.um"
	"../umbox/strings/strings.um"

	"std.um"
	"common.um"
)

fn fetchDeps(deps: []common.Box, versions: ^map[str]str): std.Err

var (
	copyDeps: bool = false
)

fn copyFile(p1, p2: str) {
	f1, err := std.fopen(p1, "rb")
	d, err := std.freadall(f1)
	f2, err := std.fopen(p2, "wb")
	std.fwrite(f2, d)
	std.fclose(f1)
	std.fclose(f2)
}

fn fetchDep(dep: common.Box, versions: ^map[str]str): std.Err {
	if !dep.exists() {
		return std.error(255, "package not found")
	}
	
	os.mkdirp(filepath.join("umbox", dep.getName()))

	// fetch and extract box.tar
	d, err := dep.download("box.tar")
	if err.code != 0 {
		return err
	}
	tf, err := tar.openBytes([]uint8(d))
	if err.code != 0 {
		return err
	}
	
	err = tf.extract(filepath.join("umbox", dep.getName(), ""))
	if err.code != 0 {
		return err
	}
	
	// Fetch dependencies
	meta, err := common.getMeta(filepath.join("umbox", dep.getName(), "box.json"))
	if err.code != 0 {
		return err
	}

	err = fetchDeps(meta.dependencies, versions)
	if err.code != 0 {
		return err
	}

	// Create symlinks for dependencies
	os.mkdirp(filepath.join("umbox", dep.getName(), "umbox"))
	for i,d in meta.dependencies {
		if copyDeps {
			os.walk(filepath.join("umbox", d.getName()), |dep, d| {
				file2 := filepath.join(
					"umbox", dep.getName(), "umbox", d.getName(),
					strings.trimprefix(file, filepath.join("umbox", d.getName()))
				)

				os.mkdirp(filepath.dir(file2))
				copyFile(file, file2)
			}, { excludeDirs: true })
		} else {
			err := os.link(filepath.join("..", "..", d.getName()), filepath.join("umbox", dep.getName(), "umbox", d.getName()))
			if err.code != 0 && os.getPlatform() == os.Platform.windows {
				std.system("mklink /j " + filepath.join("umbox", dep.getName(), "umbox", d.getName()) + " " + filepath.join("umbox", d.getName()))
			}
		}
	}

	{
		d, err := dep.download("version")
		if err.code != 0 {
			return err
		}
		versions[dep.getName()] = str([]char(d))
	}

	return {}
}

fn fetchDeps(deps: []common.Box, versions: ^map[str]str): std.Err {
	for i,dep in deps {
		// If the dependency doesn't exist, fetch it.
		if !os.isdir(filepath.join("umbox", dep.getName())) {
			fprintf(std.stderr(), "Downloading %s...\n", dep.getName())
			err := fetchDep(dep, versions)
			if err.code != 0 {
				return err
			}
			continue
		}

		// If the dependency doesn't have a known version, fetch it.
		if !validkey(versions^, dep.getName()) {
			fprintf(std.stderr(), "Downloading %s...\n", dep.getName())
			err := fetchDep(dep, versions)
			if err.code != 0 {
				return err
			}
			continue
		}

		// Check for new versions.
		{
			d, err := dep.download("version")
			if err.code != 0 {
				return err
			}
			if versions[dep.getName()] != str([]char(d)) {
				fprintf(std.stderr(), "Updating %s...\n", dep.getName())
				err = fetchDep(dep, versions)
				if err.code != 0 {
					return err
				}
				continue
			}
		}
	}

	return {}
}

fn touchDep(dep: str, touched: ^map[str]bool): std.Err {
	if validkey(touched^, dep) {
		return {}
	}

	touched[dep] = true

	meta, err := common.getMeta(filepath.join("umbox", dep, "box.json"))
	if err.code != 0 {
		return err
	}

	for i, dep in meta.dependencies {
		err = touchDep(dep.getName(), touched)
		if err.code != 0 {
			return err
		}
	}

	return {}
}

fn run*(argi: int): std.Err {
	for argi < std.argc() {
		v := std.argv(argi)

		if v == "-c" {
			copyDeps = true
			printf("NOTE: The -c flag is experimental. It is advised to always remove the umbox/\ndirectory before running update with this flag\n")
		} else {
			printf("usage: umbox update [ -c ]\n  -c: Don't use symlinks\n")
			return {}
		}

		argi++
	}

	meta, err := common.getMeta("box.json")
	if err.code != 0 {
		return err
	}
	
	os.mkdirp("umbox")

	versions := map[str]str{}
	if os.isfile(filepath.join("umbox", "versions.json")) {
		f := std.fopen(filepath.join("umbox", "versions.json"), "r").item0
		r := map[str]any(json.parse(str(std.freadall(f).item0)))
		std.fclose(f)
		for k,v in r {
			versions[k] = str(v)
		}
	}

	err = fetchDeps(meta.dependencies, &versions)
	if err.code != 0 {
		return err
	}

	touched := map[str]bool{}
	for i,dep in meta.dependencies {
		err = touchDep(dep.getName(), &touched)
		if err.code != 0 {
			return err
		}
	}
		
	enc := jsonenc.mk(true)
	enc.startObject()
	for k,v in versions {
		if touched[k] {
			enc.putKey(k)
			enc.putVal(v)
		}
	}
	enc.endObject()

	f := std.fopen(filepath.join("umbox", "versions.json"), "w").item0
	fprintf(f, "%s", enc.toStr())
	std.fclose(f)
	
	ls, _ := os.listdir("umbox")
	for i,d in ls {
		if d == "versions.json" || d == "." || d == ".." {
			continue
		}

		if !touched[d] {
			fprintf(std.stderr(), "Removing %s...\n", d)
			os.walk(filepath.join("umbox", d), fn(path: str) |d| {
				err := os.remove(path)
				if err.code != 0 {
					fprintf(std.stderr(), "Error removing %s: %s\n", path, err.msg)
				}
			}, { skipLinks: true })
			
			os.remove(filepath.join("umbox", d))
		}
	}

	return {}
}
