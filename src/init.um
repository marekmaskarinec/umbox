
import (
	"common.um"
	"update.um"
	"std.um"
        
	"../umbox/tar/tar.um"
	"../umbox/filepath/filepath.um"
	"../umbox/io/io.um"
	"../umbox/os/os.um"
)

fn downloadPreset(packageName: str): (common.Meta, str) {
	if !common.exists(packageName, "init.tar") {
		return {}, sprintf("Box '%s' doesn't have an init preset", packageName)
	}

	dat, ok, msg := common.download(packageName, "init.tar")
	if !ok {
		error(msg)
	}

	tf, err := tar.openBytes(dat)
	if err != 0 {
		return {}, tar.strerror(err)
	}

	err = tf.extract(".")
	if err != 0 {
		return {}, tar.strerror(err)
	}

	ok, m := common.getMeta("box.json")
	if !ok {
		return m, "box.json is invalid"
	}
	return m, ""
}

fn run*(url: str, argi: int) {
	if std.argc() < 2 {
		fprintf(std.stderr(), "Usage: umbox init [<preset>]\n")
		return
	}
	
	if ls, err := os.listdir("."); len(ls) > 2 {
		fprintf(std.stderr(), "%v\n", ls)
		fprintf(std.stderr(), "Refusing to init a box in a non-empty directory\n")
		return
	}

	m := common.Meta{
		version: "v0.1.0",
		readme: "README.md"
	}

	if std.argc() == 3 {
		preset := std.argv(argi)
		if !common.exists(preset) {
			fprintf(std.stderr(), "Box '%s' doesn't exist\n", preset)
			return
		}

		var err: str
		m, err = downloadPreset(preset)
		if len(err) != 0 {
			fprintf(std.stderr(), "Could not download preset: %s\n", err)
			return
		}
	}

	name, err := os.getCwd()
	m.name = filepath.file(name)

	f := std.fopen("box.json", "w")
	fprintf(f, "%s", m.toJSON())
	std.fclose(f)

	update.run(url, std.argc())
}
