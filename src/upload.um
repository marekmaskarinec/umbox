
import (
	"std.um"
	"../umbox/os/os.um"
	"../umbox/http/http.um"
	"../umbox/filepath/filepath.um"
	"common.um"
)

fn run*(token, file: str): std::Err {
	if !os::isfile(file) {
		return common::error(.fileNotFound, file)
	}
		   
	if len(token) != 64 {
		return common::error(.secret)
	}
	
	meta, err := common::getMeta("box.json")
	if err.code != 0 {
		return err
	}

	f, err := std::fopen(file, "rb")
	if err.code != 0 {
		return err
	}

	reqUrl := sprintf("api/package/%s/upload/%s", meta.name, filepath::file(file))
	resp := common::post(std::freadall(f).item0, common::serverUrl, reqUrl, token)
	if !resp.ok {
		return common::error(.request, resp.msg)
	}

	err = std::fclose(f)
	if err.code != 0 {
		return err
	}

	if resp.status != 200 {
		return common::error(.request, std::itoa(resp.status))
	}

	return {}
}
